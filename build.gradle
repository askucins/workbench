//TODO Fix these dummy workarounds for Intellij
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.ivy:ivy:2.5.3'
    }
}


plugins {
    id 'com.github.erdi.idea-base' version '2.2'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

wrapper {
    gradleVersion = '8.14.3'
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    repositories {
        mavenCentral()
    }

    /*
        Inspired by https://solidsoft.wordpress.com/2014/11/13/gradle-tricks-display-dependencies-for-all-subprojects-in-multi-project-build/

        For example:
        gw allDeps --configuration implementation
        gw allDeps --configuration testImplementation
        gw allDeps --configuration api
    */
    task allDeps(type: DependencyReportTask) {}
}

asciidoctor {
    sourceDir file('.')
    sources {
        include 'readme.adoc'
    }
    outputDir file('build/docs')
}

subprojects {
    version = '0.1'
}

// Effectively a list of projects below is all subprojects. 
// In that case instead of configure(...) syntax one could use simply 'subprojects'
// That configure(...) is useful when one needs to apply it only to a subset of subprojects.

configure(subprojects.findAll {
    [
        'fun-with-groovy',
        'geb-bare-bone',
        'grokalg',
        'misc',
        'parameterize',
        'run-me'
    ].contains(it.name)
}) {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'idea'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencies {
        implementation 'org.apache.groovy:groovy-all:4.0.30'
        implementation 'org.apache.groovy:groovy-json:4.0.30'
        implementation 'org.apache.groovy:groovy-yaml:4.0.30'
        implementation 'org.apache.groovy:groovy-xml:4.0.30'
        implementation 'org.apache.groovy:groovy-ginq:4.0.30'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'ch.qos.logback:logback-classic:1.5.18'
        def spockVersion = '2.4-M6-groovy-4.0'
        implementation "org.spockframework:spock-core:${spockVersion}", {
            exclude group: 'org.apache.groovy'
        }
        implementation 'org.apache.commons:commons-text:1.14.0'
        implementation 'commons-io:commons-io:2.20.0'

        testImplementation('com.athaydes:spock-reports:2.5.1-groovy-4.0') {
            transitive = false
        }

        // TODO check https://github.com/AOEpeople/geb-spock-reports
        testImplementation('com.aoe:geb-spock-reports:0.3.0-RC1') {
            exclude group: 'org.codehaus.groovy'
        }

        // New Junit...
        testImplementation 'org.junit.platform:junit-platform-testkit:1.13.4'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'

    }

    tasks.withType(GroovyCompile).configureEach {
        options.incremental = true
        options.incrementalAfterFailure = true
        doFirst {
            logger.lifecycle 'Explicitly enabling incremental compilation :{} in task: {}', options.incremental, it.name
        }
        //See: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.GroovyCompileOptions.html
        configure(groovyOptions.forkOptions) {
            memoryInitialSize = '512m'
            memoryMaximumSize = '6g'
            jvmArgs = ['-Xms512m', '-Xmx6g']
        }
    }

    //TODO read more about that 'configureEach' in https://docs.gradle.org/current/userguide/task_configuration_avoidance.html
    tasks.withType(Test).configureEach {
        // See https://blog.gradle.org/stop-rerunning-tests
        systemProperty "random.testing.seed", new Random().nextInt()

        testLogging {
            showStandardStreams = true
            events 'passed', 'failed'
            exceptionFormat 'full'

            debug {
                events 'passed', 'skipped', 'failed'
                showStackTraces true
                exceptionFormat 'full'
            }
        }
        useJUnitPlatform()

        reports {
            String reportTimestamp = System.currentTimeMillis().toString()
            html.outputLocation = file("${reports.html.outputLocation.get()}-${reportTimestamp}")
            junitXml.outputLocation = file("${reports.junitXml.outputLocation.get()}-${reportTimestamp}")
            //Gotcha! This is the actual source for other reports!
            binaryResultsDirectory = file("${binaryResultsDirectory.get()}-${reportTimestamp}")
        }

        doFirst {
            logger.lifecycle "... configured test report (dir): {}", reports.html.outputLocation.get()
            logger.lifecycle "... configured test report (xml): {}", reports.junitXml.outputLocation.get()
            logger.lifecycle "... configured test report (bin): {}", binaryResultsDirectory.get()
        }
    }


    tasks.register('removeTestResults', Delete) {
        group = 'other'
        description = 'removes reports and logs'
        delete "reports"
        delete "logs"
        delete "build/reports"
        delete "build/test-results"
    }

    tasks.register('showSomeConfig') {
        group = 'other'
        description = 'shows some configuration'
        doFirst {
            logger.lifecycle "List of dirs with test classes for project {}", project
            FileCollection testDirs = project.sourceSets.test.output.classesDirs
            List<String> paths = testDirs.collect { File file -> file.path }
            logger.lifecycle ">>> ${paths}"
        }
    }
}
