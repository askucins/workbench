plugins {
    id 'org.asciidoctor.convert' version '2.4.0' apply false
}

wrapper {
    gradleVersion = '6.7.1'
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    repositories {
        jcenter()
    }

    /*
    Inspired by https://solidsoft.wordpress.com/2014/11/13/gradle-tricks-display-dependencies-for-all-subprojects-in-multi-project-build/

    For example:
    gw allDeps --configuration implementation
    gw allDeps --configuration testImplementation
    gw allDeps --configuration api
 */
    task allDeps(type: DependencyReportTask) {}
}

subprojects {
    version = '0.1'
}

// Effectively a list of projects below is all subprojects. 
// In that case instead of configure(...) syntax one could use simply 'subprojects'
// That configure(...) is useful when one needs to apply it only to a subset of subprojects.

configure(subprojects.findAll {
    ['fun-with-groovy', 'geb-bare-bone', 'grokalg', 'misc', 'parameterize', 'run-me'].contains(it.name)
}) {
    apply plugin: 'groovy'

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
        implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
        implementation 'org.codehaus.groovy:groovy-all:3.0.5'
        testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-3.0', {
            exclude module: 'groovy-all'
        }
        testImplementation 'org.spockframework:spock-junit4:2.0-M3-groovy-3.0' //Temporary workaround for JUnit 4 rules
        // New Junit...
        testImplementation 'org.junit.platform:junit-platform-testkit:1.7.0-RC1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0-RC1'

    }
}

subprojects {
    tasks.withType(Test) {

        useJUnitPlatform()

        testLogging {
            events 'passed', 'failed'
            exceptionFormat 'short'

            debug {
                events 'passed', 'skipped', 'failed'
                showStackTraces true
                exceptionFormat 'full'
            }
        }
    }
}
