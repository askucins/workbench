plugins {
    id 'org.asciidoctor.convert' version '1.5.9' apply false
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    version = '0.1'
}

// Effectively a list of projects below is all subprojects. 
// In that case instead of configure(...) syntax one could use simply 'subprojects'
// That configure(...) is useful when one needs to apply it only to a subset of subprojects.

configure(subprojects.findAll {
    ['fun-with-groovy', 'run-me', 'misc'].contains(it.name)
}) {
    apply plugin: 'groovy'

    dependencies {
        compile 'org.codehaus.groovy:groovy:2.5.5'
        compile "org.slf4j:slf4j-api:1.8.0-beta2"
        compile "ch.qos.logback:logback-classic:1.3.0-alpha4"
        testCompile 'org.spockframework:spock-core:1.2-groovy-2.5', {
            exclude module: 'groovy-all'
        }
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            events 'passed', 'failed'
            exceptionFormat 'short'

            debug {
                events 'passed', 'skipped', 'failed'
                showStackTraces true
                exceptionFormat 'full'
            }
        }
    }
}