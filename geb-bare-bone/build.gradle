//TODO This is only a workaround to stop Intellij complaining about those imports below (they work anyway)!
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.groovy:groovy-json:4.0.22'
    }
}

plugins {
    id 'com.github.erdi.webdriver-binaries' version '3.2'
}

import groovy.json.JsonBuilder
import groovy.json.JsonOutput
import groovy.json.JsonSlurper

/**
 * Geb and Webdriver mgmt
 * Properties:
 *  qa.webdriver
 *  qa.altchrome
 */

tasks.withType(Test) {
    systemProperty 'geb.env', project.findProperty('qa.webdriver')
    systemProperty 'geb.build.reportsDir', 'reports'
    systemProperty 'qa.altchrome', project.findProperty('qa.altchrome')
}

webdriverBinaries {
    driverUrlsConfiguration = resources.text.fromFile('webdriver.json')
    chromedriver {
        version = {
            (project.findProperty('qa.altchrome') != null)
                ? '123.0.6312.122' //TC
                : '128.0.6613.86' //Laptop
        }()
        fallbackTo32Bit = true
    }
    geckodriver '0.33.0'
}

task refreshWebdriverConfiguration {
    group = 'other'
    description = 'refreshes configuration of webdriver binaries'

    def slurper = new JsonSlurper()
    def customWebdriver = slurper.parseText(resources.text.fromFile('webdriver-custom.json').asFile().text)
    try {
        def remoteWebdriver = slurper.parseText(new URL('https://raw.githubusercontent.com/webdriverextensions/webdriverextensions-maven-plugin-repository/master/repository-3.0.json').text)
        def finalWebdriver = slurper.parseText(new JsonBuilder([drivers: customWebdriver.drivers + remoteWebdriver.drivers]).toString())
        def webdriverJson = new File(projectDir, 'webdriver.json')
        webdriverJson.delete()
        webdriverJson << JsonOutput.prettyPrint(JsonOutput.toJson(finalWebdriver))
    } catch (e) {
        logger.error("Unable to refresh webdriver versions configuration!")
        logger.error(e.asString())
    }
}

task removeWebdriverInstances() {
    group = 'other'
    description = 'removes hanging instances of webdriver'

    def doRemoveWebdriverInstances = {
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "webdriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "chromedriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "marionette"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "webdriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "chromedriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "marionette"
        }
    }

    doLast {
        doRemoveWebdriverInstances()
    }
}

task cleanGebTestResults(type: Delete) {
    group = 'other'
    description = 'removes geb reports and logs'
    delete "reports"
    delete "logs"
}

/**
 *  Usual configuration
 */

repositories {
    mavenCentral()
}

dependencies {
    def gebVersion = '7.0'
    implementation "org.gebish:geb-core:${gebVersion}"
    testImplementation "org.gebish:geb-spock:${gebVersion}"
    def webdriverVersion = '4.24.0'
    implementation 'io.netty:netty-buffer:4.1.89.Final' // TODO see below - workaround
    implementation("org.seleniumhq.selenium:selenium-support:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-chrome-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-chromium-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-firefox-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-devtools-v128:${webdriverVersion}") {
        exclude group: 'io.netty'
    }

    testImplementation('com.athaydes:spock-reports:2.5.0-groovy-4.0') {
        transitive = false
    }

    // TODO check https://github.com/AOEpeople/geb-spock-reports
    testImplementation ('com.aoe:geb-spock-reports:0.3.0-RC1') {
        exclude group: 'org.codehaus.groovy'
    }

    implementation('com.assertthat:selenium-shutterbug:1.6') {
        exclude group: 'org.seleniumhq.selenium'
        exclude group: 'org.codehaus.groovy'
    }

    // Proxy unmaintained: https://github.com/browserup/browserup-proxy
    // Replaced by https://github.com/valfirst/browserup-proxy
    //TODO Replace finally by https://github.com/browserup/mitmproxy
    def versionBrowserUp = '2.2.18'
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy:${versionBrowserUp}"
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy-core:${versionBrowserUp}"
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy-mitm:${versionBrowserUp}"

    // Proxy https://github.com/LittleProxy/LittleProxy
    implementation 'io.github.littleproxy:littleproxy:2.2.3'

    // Decompressing Brotli-encoded payload
    implementation 'org.brotli:dec:0.1.2'
}